buildscript {
    repositories {
        gradlePluginPortal()
        mavenLocal()
        mavenCentral()
        maven { url "https://jitpack.io" }
        google()
    }
    dependencies {
        classpath "com.neenbedankt.gradle.plugins:android-apt:1.7"
        classpath "com.karumi:shot:5.14.1"
        classpath "com.trevjonez.composer:plugin:1.0.0-rc08"
    }
}

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "com.trevjonez.composer"
apply plugin: "shot"

android {
    compileSdkVersion 33
    defaultConfig {
        applicationId "com.karumi"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "com.karumi.TestRunner"
        multiDexEnabled true
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_11
        sourceCompatibility JavaVersion.VERSION_11
    }
    testBuildType "customBuildType"
    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            applicationIdSuffix = ".releaseSuffix"
        }
        register("customBuildType") {
            initWith debug
        }
    }
    packagingOptions {
        exclude "META-INF/*"
    }
    flavorDimensions "color"
    productFlavors {
        blue {
            dimension "color"
            testApplicationId "com.test.application.id.test"
        }
        green {
            applicationIdSuffix ".green"
            dimension "color"
        }
    }
    testOptions {
        if (System.getenv("orchestrated") == "true") {
            execution "ANDROIDX_TEST_ORCHESTRATOR"
        }
    }
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.29.0"

    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'

    implementation "com.github.salomonbrys.kodein:kodein:4.1.0"
    implementation "com.github.salomonbrys.kodein:kodein-core:4.1.0"
    implementation "com.github.salomonbrys.kodein:kodein-android:4.1.0"
    implementation "com.github.salomonbrys.kodein:kodein-conf:4.1.0"
    implementation "com.squareup.picasso:picasso:2.5.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    annotationProcessor 'androidx.lifecycle:lifecycle-compiler:2.0.0'

    testImplementation "junit:junit:4.13"
    androidTestUtil "androidx.test:orchestrator:1.4.1"

    androidTestImplementation "androidx.test:runner:1.3.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"
    androidTestImplementation "androidx.test.espresso:espresso-intents:3.3.0"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:3.3.0"
    androidTestImplementation "androidx.test:rules:1.3.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "asm:asm:3.3.1"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.1.1"
    androidTestImplementation "com.github.tmurakami:dexopener:2.0.0"
    androidTestImplementation "org.mockito:mockito-android:2.28.2"
    androidTestImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"
    androidTestImplementation "net.yslibrary.keyboardvisibilityevent:keyboardvisibilityevent:2.0.0"
}

tasks.register("ktlint", JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

tasks.named("check") { dependsOn "ktLint" }

tasks.register("ktlintFormat", JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.AbstractKotlinCompile).configureEach {
    kotlinOptions.freeCompilerArgs += ["-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi"]
}

shot {
    showOnlyFailingTestsInReports = true
}
