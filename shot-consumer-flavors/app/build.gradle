buildscript {
  repositories {
    gradlePluginPortal()
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url uri("../../repo") }
    jcenter()
    google()
  }
  dependencies {
    classpath "com.neenbedankt.gradle.plugins:android-apt:1.7"
    classpath "com.karumi:shot:5.5.1"
    classpath "com.trevjonez.composer:plugin:1.0.0-rc08"
  }
}

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "com.trevjonez.composer"
apply plugin: "shot"

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.karumi"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "com.karumi.TestRunner"
        multiDexEnabled true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            applicationIdSuffix = ".releaseSuffix"
        }
    }
    packagingOptions {
        exclude "META-INF/*"
    }
    flavorDimensions "color"
    productFlavors {
        blue {
            dimension "color"
            testApplicationId "com.test.application.id.test"
        }
        green {
            applicationIdSuffix ".green"
            dimension "color"
        }
    }
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.29.0"

    implementation "com.android.support:appcompat-v7:28.0.0"
    implementation "com.android.support:design:28.0.0"

    implementation "com.github.salomonbrys.kodein:kodein:4.1.0"
    implementation "com.github.salomonbrys.kodein:kodein-core:4.1.0"
    implementation "com.github.salomonbrys.kodein:kodein-android:4.1.0"
    implementation "com.github.salomonbrys.kodein:kodein-conf:4.1.0"
    implementation "com.squareup.picasso:picasso:2.5.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1"
    implementation "android.arch.lifecycle:extensions:1.1.1"
    annotationProcessor "android.arch.lifecycle:compiler:1.1.1"

    testImplementation "junit:junit:4.13"

    androidTestImplementation "androidx.test:runner:1.3.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"
    androidTestImplementation "androidx.test.espresso:espresso-intents:3.3.0"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:3.3.0"
    androidTestImplementation "androidx.test:rules:1.3.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "asm:asm:3.3.1"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.1.1"
    androidTestImplementation "com.github.tmurakami:dexopener:2.0.0"
    androidTestImplementation "org.mockito:mockito-android:2.28.2"
    androidTestImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"
    androidTestImplementation "net.yslibrary.keyboardvisibilityevent:keyboardvisibilityevent:2.0.0"
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.AbstractKotlinCompile).all {
    kotlinOptions.freeCompilerArgs += ["-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi"]
}

shot {
    showOnlyFailingTestsInReports = true
}

